group 'zone.fryd.sdk'
version '0.8.8'

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.4'

    compile group: 'com.github.scribejava', name: 'scribejava-core', version: '5.3.0'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
    compile group: 'org.json', name: 'json', version: '20180130'
    compile group: 'com.google.guava', name: 'guava', version: '24.1-jre'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'fryd Java SDK'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'An SDK for using the fryd.zone API. Bring gamification to your application.'
                url 'https://www.fryd.zone'

                scm {
                    url 'https://github.com/frydzone/frydSDK.git'
                    connection 'scm:git@https://github.com/frydzone/frydSDK.git'
                    developerConnection 'scm:git@https://github.com/frydzone/frydSDK.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'd4rk463'
                        name 'Kristoffer PÃ¶pperling'
                        email 'poeppi@fryd.zone'
                    }
                }
            }
        }
    }
}