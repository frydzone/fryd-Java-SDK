group 'com.fryd.sdk'
version '0.8.8'

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.4'

    compile group: 'com.github.scribejava', name: 'scribejava-core', version: '5.3.0'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
    compile group: 'org.json', name: 'json', version: '20180130'
    compile group: 'com.google.guava', name: 'guava', version: '24.1-jre'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

def pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'd4rk463'
            name 'Kristoffer PÃ¶pperling'
            email 'poeppi@fryd.zone'
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

jar {
    from rootProject.file('LICENSE')
    from rootProject.file('README.md')
}

publishing {
    publications {
        binaryAndSources(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'fryd.zone Java SDK BETA.')
                root.children().last() + pomConfig
            }
        }
    }
}


repositories {
    mavenLocal()

//    jcenter()
}
